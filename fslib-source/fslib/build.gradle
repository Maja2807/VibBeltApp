apply plugin: 'com.android.library'

ext {
    fslibVersion = "2.1.1"
    fslibBuild = 11

    fslibArchiveBaseName = "fslib-android"
    fslibArchiveName = "${fslibArchiveBaseName}-${fslibVersion}-b${fslibBuild}.aar"
    fslibGroupId = "de.feelspace"
    fslibArtifactId = "fslib"
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode fslibBuild
        archivesBaseName = fslibArchiveBaseName
        versionName fslibVersion
        project.version = fslibVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /*
     * Rename the .AAR with the base name, version name and build (version code).
     */
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('-release.aar')) {
                outputFileName = fslibArchiveName
            }
        }
    }

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
//    classpath += configurations.javadocDeps
    failOnError false
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
//    javadocDeps 'com.android.support:support-annotations:27.1.1'
}

apply from: 'fslib-bintray-deploy.gradle'