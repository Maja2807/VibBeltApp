apply plugin: 'com.android.library'

ext {
    fslibVersion = "2.2.1"
    fslibBuild = 13
    fslibArchiveBaseName = "fslib-android"
    fslibReleaseArchiveName = "${fslibArchiveBaseName}-${fslibVersion}-b${fslibBuild}-release.aar"
    fslibGroupId = "de.feelspace"
    fslibArtifactId = "fslib"
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 29
        versionCode fslibBuild
        archivesBaseName = fslibArchiveBaseName
        versionName fslibVersion
        project.version = fslibVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /*
     * Rename the .AAR with the base name, version name and build (version code).
     */
//    libraryVariants.all { variant ->
//        variant.outputs.all { output ->
//            if (outputFile != null && outputFileName.endsWith('-release.aar')) {
//                outputFileName = fslibReleaseArchiveName
//            }
//        }
//    }
    afterEvaluate {
        android.libraryVariants.all { variant ->
            variant.variantData.outputFactory.apkDataList.each { apkData ->
                if (apkData.outputFileName.endsWith('-release.aar')) {
                    apkData.outputFileName = fslibReleaseArchiveName
                }
            }
        }
    }

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.annotation:annotation:1.1.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

//apply from: 'fslib-bintray-deploy.gradle'